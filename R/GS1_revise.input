Data    :   Input file for GS1 under-determined LIM
Resp type:  maintenance respiration+BAC:	 30% of TOU (Mahaut et al. 1995)
Units   :   Fluxes: mg C /m2/d
Standing stocks:    mg C /m2

Tung 2023.01.05

#################################################################

################################ PARAMETERS 
! These are used later to increase the readability of the file (see below)
!-- Tlim
Tlim    =1.09

!-- Maintenance Respiration
MEImr= Tlim * 0.01 * MEISS	           
MACmr= Tlim * 0.01 * MACSS


!-- Stocks
DETSS   	= 540984.6     {mgC/m2}
BACSS		= 42.80	{mgC/m2}
MEISS		= 46.41	{mgC/m2}
MACSS		= 50.03	{mgC/m2}

!-- Constraints
!growth efficiency
minBACge  	= 0.02   	{fraction}
maxBACge  	= 0.61	{fraction}

!assimilation efficiency
minMEIae  	= 0.456   	{fraction}
maxMEIae  	= 0.699	{fraction}
minMACae  	= 0.6  	{fraction}
maxMACae  	= 0.7	{fraction}

!PB ratio
minMEIpb  	=  0.009   		{fraction}
maxMEIpb  	=  0.0493		{fraction}
minMACpb  	=  0.000849	{fraction}
maxMACpb  	=  0.00477	{fraction}

!net growth efficiency
minMEInge	= 0.3	{fraction}
maxMEInge	= 0.5	{fraction}
minMACnge  	= 0.6	{fraction}
maxMACnge  	= 0.72	{fraction}

!calculated respiration
minBACres	=0.0		{mgC/m2/d}
maxBACres	=15.526	{mgC/m2/d}


!burial rate efficiency
minBR  	= 0.6581141	{fraction}
maxBR  	= 1        {fraction}

!export rate
minER  	= 0.0		{fraction}
maxER  	= 0.87        {fraction}

!POC
minPOC =52.5	{mgC/m2/d}
maxPOC =153.3	{mgC/m2/d}

!mortality
minMEImor = 0
maxMEImor =Tlim * 0.2 * MEISS
minMACmor = 0
maxMACmor =Tlim * 0.05 * MEISS

!virallysis
minVir=0	{fraction}
maxVir=0.57	{fraction}
################################ END PARAMETERS                     

################################ STOCK 
! Definition of the compartments and their magnitude
! Values are defined in the parameter section
! Mass balances are set up for these compartments

DET 	= DETSS
BAC 	= BACSS       
MEI 	= MEISS    
MAC 	= MACSS

################################ END STOCK

################################ EXTERNALS 
! Definition of the compartments that are part of 
! the world external to the food web.
! No mass balances for these compartments are defined in the LIM

POC_W
DIC_W
PRE
MEI_MOR
MAC_MOR
MEI_DEF
MAC_DEF
BUR
EXP
DOC

################################ END EXTERNALS

################################ FLOW 

!-- SEDiment
POC_W  ->  DET	!POC particle rain to Detritus 
DET  ->  BUR	!Sediment burial
DET  ->  EXP    !Sediment export


!microbial loop
DET -> DOC	!Detritus degrade to DOC
DOC -> BAC	!DOC uptaken by bacteria
BAC -> DOC	!viral lysis

!-- BACteria
BAC  ->  MEI	!bac grazed by meiofauna
BAC  ->  MAC	!bac grazed by macrofauna
BAC  ->  DIC_W 	!bacteria respiration

!-- MEIofauna
DET  ->  MEI 	!Detritus uptaken by meiofauna
MEI  ->  MAC 	!meiofauna preyed by macro
MEI  ->  DIC_W 	!meio respiration
MEI  ->  PRE 	!meiofauna preyed by predator
MEI  ->  MEI_DEF 	!meiofauna defecation 
MEI  ->  MEI_MOR 	!meiofauna mortality

! MACrobenthos
DET  ->  MAC 	!Detritus uptaken by macrofauna
MAC  ->  DIC_W 	!macro respiration
MAC  ->  PRE 	!macrofauna preyed by predator
MAC  ->  MAC_DEF 	!meiofauna defecation 
MAC  ->  MAC_MOR 	!meiofauna mortality
################################ END FLOW

################################ RATE  
! The rate of change of each compartment
! These are zero for mass balances in steady-state

DET		=0.0       
BAC		=0.0       
MEI		=0.0
MAC		=0.0

################################ END RATE

################################ EQUATION 
! Definition of the equalities for the equality equation

################################ END EQUATION

################################ VARIABLES
! Defition of variables
! Each variable name is linked to a combination of flows 
! Variables are used to increase the readability of the equalities and inequalities (see below)

!consumption
MEIcs	= DET -> MEI + BAC -> MEI 
MACcs	= DET -> MAC + BAC -> MAC + MEI -> MAC

!growth respiration
MEIgr= MEI -> DIC_W - MEImr
MACgr= MAC -> DIC_W - MACmr

!bacteria production
BACpro= BAC->DOC + BAC->MEI + BAC->MAC
BACres= BAC->DIC_W

################################ END VARIABLES

################################ CONSTRAINT 
! Inequalities for the inequality equation
! but assuming that the reported value has an error of +/- 25%
! The numerical values (min... and max...) are defined in the parameter section (see above)
! The definition of the variable names is in the variable section (see above)

!-- DET
POC_W->DET=[minPOC,maxPOC]		!POCflux
DET->BUR = [minBR,maxBR] * POC_W->DET	!Burial
DET->EXP = [minER,maxER] * POC_W->DET	!Export

!-- BACteria
!growth efficiency
BACpro=  [minBACge,maxBACge]* BACpro+[minBACge,maxBACge]* BACres
!DOU
BAC->DIC_W= [minBACres,maxBACres]
!mortality
BAC->DOC= [minVir,maxVir]*BACSS

!-- MEI
!assimilation efficiency
MEIcs- MEI -> MEI_DEF= [minMEIae ,maxMEIae]* MEIcs 
!net growth efficiency
MEIcs- MEI->MEI_DEF - MEIgr =[minMEInge,maxMEInge]*MEIcs-[minMEInge,maxMEInge]* MEI->MEI_DEF
!production
MEIcs - MEI -> MEI_DEF - MEIgr	=  [minMEIpb,maxMEIpb]*MEISS		
!mortality
MEI -> MEI_MOR= [minMEImor,maxMEImor]

!-- MAC
!assimilation efficiency
MACcs - MAC -> MAC_DEF= [minMACae ,maxMACae]*MACcs
!net growth efficiency
MACcs- MAC->MAC_DEF - MACgr =[minMACnge,maxMACnge]*MACcs-[minMACnge,maxMACnge]* MAC->MAC_DEF
!production
MACcs - MAC -> MAC_DEF - MACgr=  [minMACpb,maxMACpb]*MACSS
!mortality
MAC -> MAC_MOR= [minMACmor,maxMACmor]
################################ END CONSTRAINT